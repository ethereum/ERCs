---
eip: 7943
title: uRWA - Universal Real World Asset Interface
description: A minimal interface for regulated assets, targeting the broad spectrum of RWAs.
author: Dario Lo Buglio (@xaler5)
discussions-to: https://ethereum-magicians.org/t/erc-universal-rwa-interface/23972
status: Draft
type: Standards Track
category: ERC
created: 2025-05-22
requires: 165
---

## Abstract

This EIP proposes "Universal RWA" (uRWA) standard, a minimal interface for all common tokens like [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) or [ERC-1155](./eip-1155.md) based tokens, meant to be the primitive for the different classes of Real World Assets. It defines essential functions and events for regulatory compliance and enforcement actions common to RWAs. It also adopts [ERC-165](./eip-165.md) for introspection.

## Motivation

The tokenization of Real World Assets introduces requirements often absent in purely digital assets, such as regulatory compliance checks, nuanced transfer controls, and potential enforcement actions. Existing token standards, primarily [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md), lack the inherent structure to address these needs directly within the standard itself.

Attempts at defining universal RWA standards historically imposed unnecessary complexity and gas overhead for simpler use cases that do not require the full spectrum of features like granular role-based access control, mandatory on-chain whitelisting, specific on-chain identity solutions or metadata handling solutions mandated by the standard.

The broad spectrum of RWA classes inherently suggests the need to move away from a one-size-fits-all solution. With the purpose in mind of defining an EIP for it, a minimalistic approach, unopinionated features list and maximally compatible design should be kept in mind.

The uRWA standard seeks a more refined balance by defining an essential interface, establishing a common ground for interaction regarding compliance and control, without dictating the underlying implementation mechanisms. This allows core token implementations to remain lean while providing standard functions for RWA-specific interactions.

The final goal is to build composable DeFi around RWAs, providing the same interface when dealing with compliance and regulation.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

**[ERC-7943](./eip-7943.md) Interface**

The following defines the standard interface for an [ERC-7943](./eip-7943.md) token contract.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.29;

/// @title Interface for the ERC7943 - uRWA Token Interface
interface IERC7943 /* is ERC165 */ {
    /// @notice Emitted when tokens are taken from one address and transferred to another.
    /// @param from The address from which tokens were taken.
    /// @param to The address to which seized tokens were transferred.
    /// @param tokenId The ID of the token being transferred.
    /// @param amount The amount seized.
    event ForcedTransfer(address indexed from, address indexed to, uint256 tokenId, uint256 amount);

    /// @notice Emitted when a specific amount of a token ID is frozen/unfrozen for a user.
    /// @param user The address of the user whose tokens are being frozen.
    /// @param tokenId The ID of the token being frozen.
    /// @param amount The amount of tokens frozen (amount > 0) or unfrozen (amount < 0).
    event FreezeStatusChange(address indexed user, uint256 indexed tokenId, int256 amount);

    /// @notice Error reverted when a user is not allowed to interact.
    /// @param account The address of the user which is not allowed for interactions.
    error ERC7943NotAllowedUser(address account);

    /// @notice Error reverted when a transfer is not allowed due to restrictions in place.
    /// @param from The address from which tokens are being transferred.
    /// @param to The address to which tokens are being transferred.
    /// @param tokenId The ID of the token being transferred.
    /// @param amount The amount being transferred.
    error ERC7943NotAllowedTransfer(address from, address to, uint256 tokenId, uint256 amount);

    /// @notice Error reverted when a transfer is attempted from `user` but the `amount` is bigger than available (unfrozen) tokens.
    /// @param user The address holding the tokens.
    /// @param tokenId The ID of the token being transferred.
    /// @param amount The amount being transferred.
    /// @param available The amount of tokens that are available to transfer.
    error ERC7943NotAvailableAmount(address user, uint256 tokenId, uint256 amount, uint256 available);

    /// @notice Takes tokens from one address and transfers them to another.
    /// @dev Requires specific authorization. Used for regulatory compliance or recovery scenarios.
    /// It should skip frozen status validations.
    /// @param from The address from which `amount` is taken.
    /// @param to The address that receives `amount`.
    /// @param tokenId The ID of the token being transferred.
    /// @param amount The amount to forcefully transfer.
    function forceTransfer(address from, address to, uint256 tokenId, uint256 amount) external;

    /// @notice Changes the freezing status of an `user` asset corresponding to specified `tokenId`.
    /// It can increase the amount of frozen assets (`amount` > 0) or decrease it (`amount` < 0).
    /// @dev Requires specific authorization. Frozen tokens cannot be transferred by the user.
    /// @param user The address of the user whose tokens are to be frozen/unfrozen.
    /// @param tokenId The ID of the token to freeze/unfreeze.
    /// @param amount The amount of tokens to freeze/unfreeze.
    function changeFreezeStatus(address user, uint256 tokenId, int256 amount) external;

    /// @notice Checks the frozen status of a specific token ID.
    /// @param user The address of the user.
    /// @param tokenId The ID of the token.
    /// @return result The amount >= 0 of the token ID currently frozen for the user for ERC-20 and ERC-1155 tokens, 1 or 0 for ERC-721.
    function freezeStatus(address user, uint256 tokenId) external view returns (uint256 result);

    /// @notice Checks if a transfer is currently possible according to token rules and registered plugins. It enforces validations on the frozen tokens.
    /// @dev This may involve checks like allowlists, blocklists, transfer limits, etc.
    /// @param from The address sending tokens.
    /// @param to The address receiving tokens.
    /// @param tokenId The ID of the token being transferred.
    /// @param amount The amount being transferred.
    /// @return allowed True if the transfer is allowed, false otherwise.
    function isTransferAllowed(address from, address to, uint256 tokenId, uint256 amount) external view returns (bool allowed);

    /// @notice Checks if a specific user is allowed to interact with the token.
    /// @dev This is often used for allowlist/KYC checks.
    /// @param user The address to check.
    /// @return allowed True if the user is allowed, false otherwise.
    function isUserAllowed(address user) external view returns (bool allowed);
}
```

*   The contract MUST implement the [ERC-165](./eip-165.md) `supportsInterface` function and MUST return true for the `bytes4` value `0x84c1cfb0` being it the `interfaceID` of the [ERC-7943](./eip-7943.md).
*   The `isUserAllowed`, `isTransferAllowed` and `freezeStatus` functions provide views into the implementing contract's compliance, transfer policy logic and freezing status. These three functions:
    - MUST NOT revert. 
    - MUST NOT change the storage of the contract. 
    - MAY depend on context (e.g., current timestamp or block.number).
    - The `isTransferAllowed` MUST enforce validation checks to not authorize transfers of amounts/tokenId which are greater or correspond to frozen assets.
*   The `forceTransfer` function provides a standard mechanism for forcing a transfer from a `from` to a `to` address. This function:
    - MUST directly manipulate balances or ownership to transfer the asset from `from` to `to` either by transferring or burning from `from` and minting to `to`.
    - SHOULD be restricted in access.
    - SHOULD bypass the freezing validations and update the freezing status accordingly after applying the state changes.
    - MUST perform necessary validation checks (e.g., sufficient balance/ownership of a specific token).
    - MUST emit both the standard `Transfer` event (from the base standard) and the `ForcedTransfer` event. 
    - MUST bypass checks enforced by `isTransferAllowed`.
    - MUST perform `isUserAllowed` check in the `recipient` parameter.
*   The `changeFreezeStatus` function provides a way to freeze or unfreeze assets hold by a specifi user. This is useful for temporary lock mechanism. This function:
    - MUST emit `FreezeStatusChange`.
    - SHOULD be restricted in access.
    - MUST NOT allow freezing more assets than the ones hold by the user and MUST NOT allow unfreezing of assets which have never been frozen previously.

Given the agnostic nature of the standard on the specific base token standard being used the implementation SHOULD use `tokenId = 0` for [ERC-20](./eip-20.md) based implementations, and `amount = 1` for [ERC-721](./eip-721.md) based implementations on `ForcedTransfer` and `FreezeStatusChange` events, `ERC7943NotAllowedTransfer` and `ERC7943NotAvailableAmount` errors and `forceTransfer`, `changeFreezeStatus` and `isTransferAllowed` functions. Integrators MAY decide to not enforce this, however the standard discourages it. This is considered a little tradeoff for having a unique standard interface for different token standards without overlapping syntaxes.

Implementations of this interface MUST implement the necessary functions of their chosen base standard (e.g., [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md) functionalities) and MUST also restrict access to sensitive functions like `forceTransfer` and `changeFreezeStatus` using an appropriate access control mechanism (e.g., `onlyOwner`, Role-Based Access Control). The specific mechanism is NOT mandated by this interface standard.

Integrators MUST ensure their transfer methods (`transfer`, `transferFrom`, `mint`, `burn`) respect the following validation logic, meaning that they MUST revert if any of the following validation logics return `false`:
- Public transfers (`transfer`, `transferFrom`, `safeTransferFrom`) MUST run `isTransferAllowed` and `isUserAllowed` on origin and destination addresses.
- Minting MUST run `isUserAllowed` on destination address.
- Burning MUST NOT run `isTransferAllowed` nor `isUserAllowed` on the wallet holding the tokens to burn. It MUST also run validation to prohibit burning more assets than the one unfrozen.

## Rationale

*   **Minimalism:** Defines only the essential functions (`forceTransfer`, `changeFreezeStatus`, `isUserAllowed`, `isTransferAllowed`) and associated events/errors needed for common RWA compliance and control patterns, avoiding mandated complexity or opinionated features.
*   **Flexibility:** Provides standard view functions (`isUserAllowed`, `isTransferAllowed`, `freezeStatus`) for compliance checks without dictating *how* those checks are implemented internally by the token contract. This allows diverse compliance strategies.
*   **Compatibility:** Designed as an interface layer compatible with existing base standards like [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md). Implementations extend from `IERC7943` alongside their base standard interface.
*   **RWA Essential:** Includes `forceTransfer` as a standard function, acknowledging its importance for regulatory enforcement in the RWA space, distinct from standard transfers. Mandates access control for this sensitive function.
*   **[ERC-165](./eip-165.md):** Ensures implementing contracts can signal support for this interface.

As an example, a Uniswap v4 pool can integrate with [ERC-7943](./eip-7943.md) based [ERC-20](./eip-20.md) tokens by calling `isUserAllowed` or `isTransferAllowed` within its before/after hooks to handle these assets in a compliant manner. Users can then expand these tokens with additional features to fit the specific needs of individual asset types, either with on-chain identity systems, historical balances tracking for dividend distributions, semi-fungibility with tokens metadata, etc.

### Notes on naming:

- `forceTransfer`: This is often known as either "confiscation" / "revocation" or even "recovery" which are all names related to the motivation being the feature itself. The name chosen tries to abstract away the motivation and keep a general name.
- `changeFreezeStatus` / `freezeStatus`: While designing the feature, the option to have two separate functions (`freeze` and `unfreeze` with relative `Freeze` / `Unfreeze` events) was considered. However, with the intent of keeping the EIP lean, it was combined under one same function that expect a delta amount (either positive or negative) to reflect an increase or decrease in amount of frozen assets. Additionally, given the general compatibility with fungible and non-fungible assets, names that included "amount" or "asset(s)" were considered not applicable for all cases, that's why "status" was chosen.

## Backwards Compatibility

This EIP defines a new interface standard and does not alter existing ones like [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md). Standard wallets and explorers can interact with the base token functionality of implementing contracts, subject to the rules enforced by that contract's implementation of `isUserAllowed` and `isTransferAllowed`. Full support for the [ERC-7943](./eip-7943.md) functions requires explicit integration.

## Reference Implementation

Examples of basic implementation for [ERC-20](./eip-20.md), [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md) which includes a basic whitelist for users and an enumerable role based access control:

### [ERC-20](./eip-20.md) Example

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.29;

/* required imports ... */

contract uRWA20 is Context, ERC20, AccessControlEnumerable, IERC7943 {
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant BURNER_ROLE = keccak256("BURNER_ROLE");
    bytes32 public constant ENFORCER_ROLE = keccak256("ENFORCER_ROLE");
    bytes32 public constant WHITELIST_ROLE = keccak256("WHITELIST_ROLE");

    mapping(address user => bool whitelisted) public isWhitelisted;
    mapping(address user => uint256 amount) internal _frozenTokens;

    event Whitelisted(address indexed account, bool status);
    error NotZeroAddress();
    error NotZeroAmount();

    constructor(string memory name, string memory symbol, address initialAdmin) ERC20(name, symbol) {
        /* give initialAdmin necessary roles ...*/
    }

    function isTransferAllowed(address from, address to, uint256, uint256 amount) public virtual view returns (bool allowed) {
        if (amount > balanceOf(from) - _frozenTokens[from]) return false;
        if (!isUserAllowed(from) || !isUserAllowed(to)) return false;

        return true;
    }

    function isUserAllowed(address user) public virtual view returns (bool allowed) {
        if (!isWhitelisted[user]) return false;
        
        return true;
    } 

    function freezeStatus(address user, uint256) external view returns (uint256 result) {
        result = _frozenTokens[user];
    }

    /* standard mint and burn functions with access control ...*/ 

    function changeWhitelist(address account, bool status) external onlyRole(WHITELIST_ROLE) {
        require(account != address(0), NotZeroAddress());
        isWhitelisted[account] = status;
        emit Whitelisted(account, status);
    }

    function forceTransfer(address from, address to, uint256, uint256 amount) public onlyRole(ENFORCER_ROLE) {
        require(isUserAllowed(to), ERC7943NotAllowedUser(to));

        // Directly update balances, bypassing overridden _update
        super._update(from, to, amount);

        // If more than unfrozen amount has been transferred, update frozen amount
        if(_frozenTokens[from] > balanceOf(from)) _frozenTokens[from] = balanceOf(from);
        emit ForcedTransfer(from, to, 0, amount);
    }

    function changeFreezeStatus(address user, uint256, int256 amount) public onlyRole(ENFORCER_ROLE) {
        if(amount == 0) revert NotZeroAmount();
        uint256 unsignedAmount = amount > 0 ? uint256(amount) : uint256(-amount);

        if(amount > 0) {
            uint256 available = balanceOf(user) - _frozenTokens[user];
            require(unsignedAmount <= available, InvalidFreezeAmount(user, available, unsignedAmount));
            
            _frozenTokens[user] += unsignedAmount;        
        } else if (amount < 0) {
            require(_frozenTokens[user] >= unsignedAmount, InvalidFreezeAmount(user, _frozenTokens[user], unsignedAmount));
        _   frozenTokens[user] -= unsignedAmount;
        }

        emit FreezeStatusChange(user, 0, amount);
    }

    function _update(address from, address to, uint256 amount) internal virtual override {
        if (from != address(0) && to != address(0)) { // Transfer
            require(isTransferAllowed(from, to, 0, amount), ERC7943NotAllowedTransfer(from, to, 0, amount)); // isTransferAllowed checks for frozen assets
        } else if (from == address(0)) { // Mint
            require(isUserAllowed(to), ERC7943NotAllowedUser(to));
        } else { // Burn - Can't burn more than available balance minus frozen tokens
            uint256 available = balanceOf(from) - _frozenTokens[from];
            require(amount <= available, ERC7943NotAvailableAmount(from, 0, amount, available));
        } 

        super._update(from, to, amount);
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override(AccessControlEnumerable, IERC165) returns (bool) {
        return interfaceId == type(IERC7943).interfaceId ||
            interfaceId == type(IERC20).interfaceId ||
            super.supportsInterface(interfaceId);
    }
}
```

### [ERC-721](./eip-721.md) Example

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.29;

/* required imports ... */

contract uRWA721 is Context, ERC721, AccessControlEnumerable, IERC7943 {
    /* same definitions, constructor and changeWhitelist function as before ...*/
    
    mapping(address user => mapping(uint256 tokenId => bool frozen)) internal _frozenTokens;

    function isUserAllowed(address user) public view virtual override returns (bool allowed) {
        if (!isWhitelisted[user]) return false;
        
        return true;
    }

    function isTransferAllowed(address from, address to, uint256 tokenId, uint256) public view virtual override returns (bool allowed) {
        if (_ownerOf(tokenId) != from || _ownerOf(tokenId) == address(0)) return false;
        if (!isUserAllowed(from) || !isUserAllowed(to)) return false;
        if (_frozenTokens[from][tokenId]) return false;
        return true;
    }

    function freezeStatus(address user, uint256 tokenId) external view returns (uint256 result) {
        result = _frozenTokens[user][tokenId] ? 1 : 0;
    }

    function changeFreezeStatus(address user, uint256 tokenId, int256 amount) public onlyRole(ENFORCER_ROLE) {
        if(amount == 0) revert NotZeroAmount();
        require(user == ownerOf(tokenId), IERC721Errors.ERC721InvalidOwner(user));
        uint256 unsignedAmount = amount > 0 ? uint256(amount) : uint256(-amount);

        if(amount > 0) {
            _frozenTokens[user][tokenId] = true;        
        } else if (amount < 0) {
            _frozenTokens[user][tokenId] = false;
        }

        emit FreezeStatusChange(user, tokenId, amount);
    }

    function forceTransfer(address from, address to, uint256 tokenId, uint256) public virtual override onlyRole(ENFORCER_ROLE) {
        require(to != address(0), ERC721InvalidReceiver(address(0)));
        require(isUserAllowed(to), ERC7943NotAllowedUser(to));
        address previousOwner = super._update(to, tokenId, address(0)); // Skip _update override
        require(previousOwner != address(0), ERC721NonexistentToken(tokenId));
        require(previousOwner == from, ERC721IncorrectOwner(from, tokenId, previousOwner));
        if(_frozenTokens[previousOwner][tokenId])_frozenTokens[previousOwner][tokenId] = false; // Unfreeze the token if it was frozen
        ERC721Utils.checkOnERC721Received(_msgSender(), from, to, tokenId, "");
        emit ForcedTransfer(from, to, tokenId, 1);
    }

    /* standard mint function with access control ...*/ 

    function burn(uint256 tokenId) external virtual onlyRole(BURNER_ROLE) {
        address previousOwner = _update(address(0), tokenId, _msgSender()); 
        if (_frozenTokens[previousOwner][tokenId]) revert ERC7943NotAvailableAmount(previousOwner, tokenId, 1, 0);
        if (previousOwner == address(0)) {
            revert ERC721NonexistentToken(tokenId);
        }
    }

    function _update(address to, uint256 value, address auth) internal virtual override returns(address) {
        address from = _ownerOf(value);

        if (auth != address(0)) {
            _checkAuthorized(from, auth, value);
        }

        if (from != address(0) && to != address(0)) { // Transfer
            require(isTransferAllowed(from, to, value, 1), ERC7943NotAllowedTransfer(from, to, value, 1));
        } else if (from == address(0)) { // Mint
            require(isUserAllowed(to), ERC7943NotAllowedUser(to));
        } else { // Burn - Frozen status is checked in the burn function
        } 

        return super._update(to, value, auth);
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override(AccessControlEnumerable, ERC721, IERC165) returns (bool) {
        return interfaceId == type(IERC7943).interfaceId ||
               super.supportsInterface(interfaceId);
    }
}
```

### [ERC-1155](./eip-1155.md) Example

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.29;

/* required imports ... */

contract uRWA1155 is Context, ERC1155, AccessControlEnumerable, IERC7943 {

    /* same definitions, constructor and changeWhitelist function as before ...*/

    mapping(address user => mapping(uint256 tokenId => uint256 amount)) internal _frozenTokens;

    function isTransferAllowed(address from, address to, uint256 tokenId, uint256 amount) public view virtual override returns (bool allowed) {
        if (balanceOf(from, tokenId) < amount) return false;
        if (!isUserAllowed(from) || !isUserAllowed(to)) return false;
        if (amount > balanceOf(from, tokenId) - _frozenTokens[from][tokenId]) return false;
        return true;
    }

    function isUserAllowed(address user) public view virtual override returns (bool allowed) {
        if (!isWhitelisted[user]) return false;
        
        return true;
    }

    function freezeStatus(address user, uint256 tokenId) external view returns (uint256 result) {
        result = _frozenTokens[user][tokenId];
    }

    function changeFreezeStatus(address user, uint256 tokenId, int256 amount) public onlyRole(ENFORCER_ROLE) {
        if(amount == 0) revert NotZeroAmount();
        uint256 unsignedAmount = amount > 0 ? uint256(amount) : uint256(-amount);

        if(amount > 0) {
            uint256 available = balanceOf(user,tokenId) - _frozenTokens[user][tokenId];
            require(unsignedAmount <= available, InvalidFreezeAmount(user, tokenId, available, unsignedAmount));
            
            _frozenTokens[user][tokenId] += unsignedAmount;
        } else if (amount < 0) {
            require(_frozenTokens[user][tokenId] >= unsignedAmount, InvalidFreezeAmount(user, tokenId, _frozenTokens[user][tokenId], unsignedAmount));

            _frozenTokens[user][tokenId] -= unsignedAmount;        
        }

        emit FreezeStatusChange(user, tokenId, amount);
    }

    function forceTransfer(address from, address to, uint256 tokenId, uint256 amount) public onlyRole(ENFORCER_ROLE) {
        require(isUserAllowed(to), ERC7943NotAllowedUser(to));

        // Reimplementing _safeTransferFrom to avoid the check on _update
        if (to == address(0)) {
            revert ERC1155InvalidReceiver(address(0));
        }
        if (from == address(0)) {
            revert ERC1155InvalidSender(address(0));
        }

        uint256[] memory ids = new uint256[](1);
        uint256[] memory values = new uint256[](1);
        ids[0] = tokenId;
        values[0] = amount;

        super._update(from, to, ids, values);
        
        if (to != address(0)) {
            address operator = _msgSender();
            if (ids.length == 1) {
                uint256 id = ids[0];
                uint256 value = values[0];
                ERC1155Utils.checkOnERC1155Received(operator, from, to, id, value, "");
            } else {
                ERC1155Utils.checkOnERC1155BatchReceived(operator, from, to, ids, values, "");
            }
        }

        if(_frozenTokens[from][tokenId] > balanceOf(from, tokenId)) _frozenTokens[from][tokenId] = balanceOf(from, tokenId);

        emit ForcedTransfer(from, to, tokenId, amount);
    }

    /* standard mint and burn functions with access control ...*/ 

    function _update(address from, address to, uint256[] memory ids, uint256[] memory values) internal virtual override {
        if (ids.length != values.length) {
            revert ERC1155InvalidArrayLength(ids.length, values.length);
        }

        for (uint256 i = 0; i < ids.length; ++i) {
            if (from != address(0) && to != address(0)) { // Transfer
                require(isTransferAllowed(from, to, ids[i], values[i]), ERC7943NotAllowedTransfer(from, to, ids[i], values[i]));
            }
        }

        if (from == address(0)) { // Mint 
            require(isUserAllowed(to), ERC7943NotAllowedUser(to));
        } else if (to == address(0)) { // Burn
            for (uint256 i = 0; i < ids.length; ++i) {
                uint256 available = balanceOf(from, ids[i]) - _frozenTokens[from][ids[i]];
                require(values[i] <= available, ERC7943NotAvailableAmount(from, ids[i], values[i], available));
            }
        }

        super._update(from, to, ids, values);
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override(AccessControlEnumerable, ERC1155, IERC165) returns (bool) {
        return interfaceId == type(IERC7943).interfaceId ||
               super.supportsInterface(interfaceId);
    }
}
```

## Security Considerations

*   **Access Control for `forceTransfer` and `changeFreezeStatus`:** The security of the mechanism chosen by the implementer to restrict access to these functions is paramount. Unauthorized access could lead to asset theft. Secure patterns (multisig, timelocks) are highly recommended.
*   **Implementation Logic:** The security and correctness of the *implementation* behind `isUserAllowed`, `isTransferAllowed` and `freezeStatus` are critical. Flaws in this logic could bypass intended transfer restrictions or incorrectly block valid transfers.
*   **Standard Contract Security:** Implementations MUST adhere to general smart contract security best practices (reentrancy guards where applicable, checks-effects-interactions, etc.).

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
