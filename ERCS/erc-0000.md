---
eip: x
title: Code Index
description: A standard interface implementation for indexing smart contract bytecode on Ethereum.
author: Tim Pechersky (@peersky) <t@peersky.xyz>
discussions-to: TODO
status: Draft
type: Standards Track
category: ERC
created: 2024-07-16
---

## Abstract

This EIP defines a standard interface for indexing smart contracts on Ethereum by their bytecode hash. This enables trustless discovery and verification of contract code, facilitating use cases like bytecode signing, whitelisting, and decentralized distribution mechanisms.

## Motivation

Existing contract discovery relies on addresses, which are non-deterministic and can be obfuscated through proxies. Indexing by bytecode hash provides a deterministic and tamper-proof way to identify and verify contract code, enhancing security and trust in the Ethereum ecosystem.

Global bytecode identification is a requirement to build efficient at high scale global factories for future software distribution, that may want to rely on extensive re-use of same codebase as own dependencies.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity =0.8.20;

interface ICodeIndex {
    event Indexed(address indexed container, bytes32 indexed codeHash);
    error alreadyExists(bytes32 id, address source);

    function register(address container) external;

    function get(bytes32 id) external view returns (address);
}

/**
 * @title Byte Code Indexer Contract
 * @notice You can use this contract to index contracts by their bytecode.
 * @dev This allows to query contracts by their bytecode instead of addresses.
 * @author Tim Pechersky (@Peersky)
 */
contract CodeIndex is ICodeIndex {
    mapping(bytes32 => address) private index;

    /**
     * @notice Registers a contract in the index by its bytecode hash
     * @param container The contract to register
     * @dev `msg.codeHash` will be used
     * @dev It will revert if the contract is already indexed
     */
    function register(address container) external {
        address etalon = index[container.codehash];
        if (etalon != address(0)) {
            revert alreadyExists(container.codehash, etalon);
        }
        index[container.codehash] = container;
        emit Indexed(container, container.codehash);
    }

    /**
     * @notice Returns the contract address by its bytecode hash
     * @dev returns zero if the contract is not indexed
     * @param id The bytecode hash
     * @return The contract address
     */
    function get(bytes32 id) external view returns (address) {
        return index[id];
    }
}

```

### Deployment method

The `CodeIndex` contract is deployed at: `0xc0d31D6A64D1BE49867158ed3c25152D240b5c0B` using `CREATE2` via the deterministic deployer at `0x4e59b44847b379578588920ca78fbf26c0b4956c` with a salt of `0x5af561df3770441d8380a7c03063753e26e4104e0a0265a18b089ee76616bcd1`.

## Rationale

**Bytecode over Addresses**: Bytecode is deterministic and can be verified on-chain, while addresses are opaque and mutable.

**Reverting on re-indexing**: There is small, yet non-zero probability of hash collision attack. Allowing to index hash of bytecode coupes with this.

**Simple Interface**: The interface is minimal and focused to maximize composability and ease of implementation.

## Reference Implementation

Code Index: https://github.com/peersky/eds/blob/main/src/CodeIndex.sol

Deployment: https://github.com/peersky/eds/blob/main/deploy/01_codeIndex.ts

## Security Considerations

**Malicious Code**: The index does NOT guarantee the safety or functionality of indexed contracts. Users MUST exercise caution and perform their own due diligence before interacting with indexed contracts.

**Hash Collisions**: While extremely unlikely, hash collisions are theoretically possible. The probability can be further reduced by increasing the hash size or using a more robust hashing algorithm in future versions of this standard.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
