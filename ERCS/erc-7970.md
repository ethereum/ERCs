---
eip: 7970
title: Stateless Encrypted Communication
description: A log-based protocol for encrypted communication between two addresses on EVM chains.
author: Scott Ewing (@sewing848) <sewing848@gmail.com>
discussions-to: https://ethereum-magicians.org/t/erc-stateless-encrypted-communication-standard/24554
status: Draft
type: Standards Track
category: ERC
created: 2025-06-10
requires: 165
---

## Abstract

This ERC defines the Stateless Encrypted Communication Standard, a minimal, non-financial, gas-efficient communication protocol. It enables encrypted peer-to-peer communication through a single emitted event. The protocol maintains no on-chain state and avoids economic constructs. It assumes the use of an ECDH key exchange to establish a shared symmetric key for secure encrypted data transfer. All message semantics are interpreted and enforced off-chain by client applications.

## Motivation

This ERC proposes a shared, minimal primitive for encrypted messaging over EVM-compatible blockchains. The intent is not merely to introduce a new protocol, but to establish an interoperable, gas-efficient interface that can serve as the default on-chain transport layer for peer-to-peer encrypted communication.

Rather than prescribing a complete protocol with architectural decisions about key management, encryption algorithms, or session handling, this ERC defines only the essential primitives needed to transmit encrypted messages. This minimal approach enables maximum flexibility: applications can implement their own key exchange strategies, choose their preferred encryption methods, and add economic or spam-prevention mechanisms as needed, while maintaining interoperability through the standardized event structure.

Existing token-based and messaging systems typically rely on state mutation, require financial interactions, or assume specific application logic. There is currently no minimal, stateless messaging standard that can serve as a common substrate for decentralized communication.

This standard defines a simple, composable interface that:

- Supports ECDH-based encrypted communication and connection setup
- Emits only one event for all message types
- Leaves interpretation and enforcement to off-chain clients
- Enables privacy-preserving and censorship-resistant messaging applications

By converging on a stateless, event-based design, this ERC aims to provide a stable and composable core that enables interoperability among messaging clients, libraries, and extensions. Applications requiring additional features, such as on-chain key registries, structured session management, or protocol-enforced encryption algorithms, can layer these capabilities on top of this foundational standard while maintaining compatibility with the basic message emission interface.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

### Contract Interface

Implementations of this protocol MUST define an event named `MessageSent` that is emitted when `SendMessage()` is called.

The `MessageSent` event MUST contain `address indexed from`, `address indexed to`, `uint256 indexed messageType`, and `bytes data` in that order.

Implementations MUST define a public constant string named `NAME` and a public function named `name()` that returns its value. This supports introspection and off-chain tooling. The constant name `NAME` follows Solidity style conventions for constants, and `name()` provides a standardized programmatic getter.

To promote compatibility and discoverability, the contract interface shown below is RECOMMENDED:

```solidity
interface IERC7970 is IERC165 {

    function name() external view returns (string memory);
    function sendMessage(address to, uint256 messageType, bytes calldata data) external;
    event MessageSent(address indexed from, address indexed to, uint256 indexed messageType, bytes data);

}
```

- `from`: the sender of the message (automatically `msg.sender`)
- `to`: the intended recipient
- `messageType`: a message classifier indicating how the data payload should be interpreted
- `data`: an opaque binary payload, formatted off-chain by convention

It is RECOMMENDED to include [ERC-165](./eip-165.md) support for interface detection.

``` solidity
function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
    return interfaceId == type(IERC7970).interfaceId || super.supportsInterface(interfaceId);
}
```

### Message Type Semantics

The protocol reserves the following `messageType` values for standardized encrypted communication primitives:

| messageType | Meaning                        | Payload                                 |
|-------------|--------------------------------|-----------------------------------------|
| 0           | Connection request             | Public key + encrypted request body     |
| 1           | Connection-request response    | Public key + encrypted response body    |
| 2           | Encrypted text message         | AES-GCM encrypted message data          |


Message types 0, 1, and 2 are reserved to promote basic interoperability, but implementers MAY define and use other message types (3 and above) for custom behavior.

These types are intended to follow this general RECOMMENDED cryptographic model:

- A shared symmetric key is derived using Elliptic Curve Diffie-Hellman (ECDH) from the sender and recipient key pairs.
- Payloads are encrypted using an authenticated encryption scheme, typically AES-GCM.

Implementers MAY vary the details of symmetric key derivation (e.g., direct use of shared secret, use of HKDF, or key rotation strategies) and encryption parameters (e.g., nonce generation), so long as the overall structure and intent are preserved. However, if substantial deviations from ECDH-based key exchange or AES-GCM semantics are used, it is RECOMMENDED to define a new messageType (e.g., >= 3) to avoid misinterpretation by other clients.

All encryption, decryption, and key exchange logic MUST be handled off-chain.

## Rationale

This core protocol is intended for encrypted communication between two addresses. Multiple message types can be filtered using `eth_getLogs` or `eth_subscribe` without needing contract storage. The core protocol omits economic interactions and access controls. By emitting only a single event, the protocol reduces on-chain complexity and minimizes gas fees.

### Related EIPs

This proposal is intentionally minimal and stateless, in the spirit of [ERC-3722: Poster](./eip-3722.md), which emits plaintext posts for decentralized social media. While Poster focuses on public broadcast-style messaging, this proposed standard defines a private, encrypted peer-to-peer communication model.

The two differ not just in use case but in security assumptions: posting plaintext messages to an immutable, public blockchain poses long-term risks, especially if illegal or harmful content becomes permanently associated with a project or platform. This standard is specifically intended for encrypted communication between two addresses - not plaintext public posts.

This proposal also contrasts with [ERC-7627: Secure Messaging Protocol](./eip-7627.md), which defines a comprehensive messaging interface, including on-chain key management, encryption algorithm enumeration, and session identifiers.

By contrast, this standard is intended to facilitate basic encrypted communication between two addresses within a wide range of app environments, while offering the flexibility to incorporate other features through app-specific use of the `messageType` parameter.

## Backwards Compatibility

No backward compatibility issues found.

## Reference Implementation

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.30;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165.sol";

/// @dev This interface MAY be used with ERC-165 for discoverability.
/// @dev Supporting ERC-165 is RECOMMENDED but NOT REQUIRED.
interface IERC7970 is IERC165 {

    /// @notice A short name to identify the contract implementation
    /// @return The name of the contract
    function name() external view returns (string memory);

    /// @notice Sends a message from the caller to the specified address
    /// @param to The address to receive the message
    /// @param messageType The type of message
    /// @dev Message type 0 = connection request, 1 = connection-request response, 
    /// @dev Message type 2 = AES-GCM encrypted text, all other values discretionary
    function sendMessage(address to, uint256 messageType, bytes calldata data) external;

    /// @notice Emitted when a message is sent
    /// @param from The address from which message was sent
    /// @param to The address to which the message was sent
    /// @param messageType The message type
    event MessageSent(address indexed from, address indexed to, uint256 indexed messageType, bytes data);

}

/// @title Stateless Encrypted Communication Standard
/// @notice A log-based protocol for encrypted communication between two addresses on EVM chains.
/// @dev This contract does not store any state and emits all communication as logs.
/// @dev All semantics are interpreted off-chain.

contract ReferenceContract is IERC7970, ERC165 {

    /// @notice The name of the implementation
    string private constant NAME = "ReferenceContract";

    /// @notice A short name to identify the contract implementation
    /// @return The name of the contract
    function name() external pure returns (string memory) {
        return NAME;
    }

    // ------------------------------------------------------------------------------------
    // If the interface is not used, the `MessageSent` event MUST be included in the contract
    // ------------------------------------------------------------------------------------

    /// @notice Send a message of the given type to the specified recipient
    /// @param to The recipient address
    /// @param messageType The classifier of the message
    /// @param data The encrypted or structured payload
    function sendMessage(address to, uint256 messageType, bytes calldata data) external {
        emit MessageSent(msg.sender, to, messageType, data);
    }

    /// @notice Indicates support for the IERC7970 interface via ERC-165
    /// @param interfaceId The interface identifier, as specified in ERC-165
    /// @return true if the contract implements the specified interface
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return interfaceId == type(IERC7970).interfaceId || super.supportsInterface(interfaceId);
    }
}
```

## Security Considerations

This standard does not define a secure communication channel. It defines a minimal, stateless broadcast interface that assumes encrypted content and permits clients to coordinate how they secure that content.

All encryption, key exchange, and identity management are handled off-chain and are entirely up to the applications and users that implement this standard. The protocol assumes that encrypted messages will be transmitted using the `sendMessage()` function, but it does not specify how those messages are encrypted, how keys are generated or rotated, or how participants authenticate one another. These responsibilities lie with off-chain clients and app developers, who may adopt any cryptographic strategies suited to their needs.

The main security implications of this protocol stem from its use of the blockchain as a transport layer:

- All data emitted is public and permanent.
- Anyone can view the `MessageSent` event logs, including the sender and recipient addresses, message type, message size, and the time and frequency of message activity.
- The actual content of the message (`data`) is assumed to be encrypted. However, the metadata surrounding that encrypted message is always visible.

This introduces some risks, but it also creates new guarantees:

- There is no central server to be maintained, compromised, or shut down.
- There are no account requirements, identifiers, or dependencies on external authentication mechanisms.
- There is no ability to delete or alter messages, which eliminates backend data retention policies as a variable.
- The protocol offers a high degree of censorship resistance. If a transaction is confirmed on-chain, its message cannot be retroactively blocked.
- A system built on this protocol exposes metadata in a transparent and predictable way, unlike many centralized systems where data exposure is hidden or poorly understood. This allows users to make informed choices rather than relying on a false sense of security.

This design is not appropriate for every use case. There is no inherent spam resistance, no enforced forward secrecy, and no protocol-level protection from metadata analysis. However, implementers may mitigate these concerns through application-level techniques such as:

- Address rotation and ephemeral wallets
- Message padding or chunking to obscure length
- Dummy messages to mask activity
- Proxy relays or steganographic encoding
- App-layer resource or reputational rate limits

This protocol is best understood as a low-level, composable primitive suitable for a subset of communication scenarios, including those where censorship resistance, pseudonymity, and open-ended integration into decentralized systems are key goals. Other applications may be better served by centralized services in order to facilitate data deletion, protect metadata, or comply with regulatory environments that do not permit a blockchain-based system using end-to-end encryption.

This standard does not aim to replace those systems. Instead, it offers a minimal foundation that can support experimentation and new forms of secure communication. The overall security of any system built on this standard depends not only on the protocol, but on the assumptions, threat models, and implementation decisions of those who use it.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE).
