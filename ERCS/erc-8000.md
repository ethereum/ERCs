---
eip: 8000
title: Multi-Fungible Token
description: This is the draft for Multi-Fungible Tokens (MFT).
author: Luiswll Wang (@luiswll) <luiswll@memelabs.dev>, william <memeking@memelabs.dev>, yaron <yaron@memelabs.dev>
discussions-to: https://ethereum-magicians.org/t/erc-8000-the-multi-fungible-token/18906
status: Draft
type: Standards Track
category: ERC
created: 2024-02-22
requires: 165, 721
---

## Abstract

This is the standard for Multi-Fungible Tokens (MFT). The provided document describes the smart contract interface set
that defines the [ERC-721](./eip-721.md) compatible token standard. It also introduces a new transfer model and an approval model for
storing various types of assets.

MFT tokens include an [ERC-721](./eip-721.md) equivalent ID property to identify themselves as unique entities, enabling MFT tokens to
be transferred between addresses and operated upon in an [ERC-721](./eip-721.md) compatible manner.

MFT implements multiple slots to configure assets of types such as [ERC-721](./eip-721.md), [ERC-20](./eip-20.md), KEY, etc. Its core features include:

*MFT Management
**Flexible configuration of MFT transfer permissions, MFT types, and levels.
*Slot Management
**Flexible configuration of slot asset types, asset contract addresses, and transfer permissions.
*Asset Management
**Through slots, support storage and configuration of [ERC-721](./eip-721.md), [ERC-20](./eip-20.md), and KEY assets.
**Enable asset transfer between MFTs.
**Enable asset transfer between MFTs and EOA wallets.
**Flexible deposit and withdrawal of tokens in MFT.

Compatibility and Scalability
Fully compatible with the [ERC-721](./eip-721.md) protocol, allowing free trading on decentralized exchanges.
This EIP introduces a new token model to achieve token multi-dimensionality, including the ability for the same MFT to
configure storage for multiple types of token assets, as well as value transfers between two assets within the same slot
and value transfers from tokens to addresses.

## Motivation

The essence of tokenization is how assets are encapsulated. Traditionally, there have been two methods used: fungible
tokens ([ERC-20](./eip-20.md)) and non-fungible tokens ([ERC-721](./eip-721.md)). These correspond to homogeneous assets and unique assets,
respectively. The former is similar to stocks or currencies, while the latter is similar to art collections or game
items.

However, there is an increasing need for encapsulating a wide variety of complex assets. The existing protocol standards
do not fully meet the requirements for encapsulating such assets.

For example, an account may hold multiple fungible assets such as USD, GBP, JPY, along with a credit account, insurance
or investment contracts, and loyalty points（KEY） from a bank. Alternatively, a game character may possess fungible
tokens within the game, various equipment items, and convertible game-specific loyalty point（KEY） assets that cannot be
directly traded but can be exchanged for corresponding game value.

To address similar requirements as mentioned above, protocols like [ERC-20](./eip-20.md), [ERC-721](./eip-721.md), [ERC-1155](./eip-1155.md), and [ERC-3525](./eip-3525.md) are not
sufficient. The most effective approach is to create a multi-fungible token that combines the non-fungible
characteristics of [ERC-721](./eip-721.md) with the ability to store and manage various custom assets such as [ERC-20](./eip-20.md), [ERC-721](./eip-721.md), and
loyalty points. It maintains an NFT's overall structure and can leverage existing NFT infrastructure.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "
OPTIONAL" in this document are to be interpreted as described in RFC 2119.

**Every MFT compliant contract must implement the [ERC-721](./eip-721.md) and [ERC-165](./eip-165.md) interfaces** :

```solidity
pragma solidity ^0.8.6;

/** 
 * @title ERC-8000 Semi-Fungible Token Standard
 * Note: the ERC-165 identifier for this interface is 0x9fa8825f.
 */
interface IERC8000 is IERC721 {
    /**
     * @dev MUST emit when value of a token is transferred to another token with the same slot,
     *  including zero value transfers (_value == 0) as well as transfers when tokens are created
     *  (`_fromTokenId` == 0) or destroyed (`_toTokenId` == 0).
     * @param _fromTokenId The token id to transfer value from
     * @param _toTokenId The token id to transfer value to
     * @param _slotIndex The slot index to transfer value to
     * @param _value The transferred value
     */
    event TransferValue(uint256 indexed _fromTokenId, uint256 indexed _toTokenId, uint256 indexed _slotIndex, uint256 _value);

    /**
     * @dev MUST emits when the approval value of a token is set or changed.
     * @param _tokenId The token to approve
     * @param _slotIndex The slot to approve
     * @param _operator The operator to approve for
     * @param _value The maximum value that `_operator` is allowed to manage
     */
    event ApprovalValue(uint256 indexed _tokenId, uint256 indexed _slotIndex, address indexed _operator, uint256 _value);

    /**
     * @notice Get the number of decimals the slot
     * @return The number of decimals for value
     */
    function slotDecimals(uint256 _slotIndex) external view returns (uint8);

    /**
     * @notice Get the balance of slot.
     * @param _tokenId The token for which to query the balance
     * @param _slotIndex The slot for which to query the balance
     * @return The value of `_slotIndex`
     */
    function balanceOf(uint256 _tokenId, uint256 _slotIndex) external view returns (uint256);

    /**
    * @dev Gets the number of NFTS in the slot
    * @param tokenId_ MFT ID
    * @param slotIndex_ Slot index
    */
    function nftBalanceOf(uint256 tokenId_, uint256 slotIndex_) external view returns (uint256[] memory);

    /**
     * @notice Allow an operator to manage the value of a token, up to the `_value` amount.
     * @dev MUST revert unless caller is the current owner, an authorized operator, or the approved
     *  address for `_tokenId`.
     *  MUST emit ApprovalValue event.
     * @param _tokenId The token to approve
     * @param _slotIndex The slot to approve
     * @param _operator The operator to be approved
     * @param _value The maximum value of `_toTokenId` that `_operator` is allowed to manage
     */
    function approve(
        uint256 _tokenId,
        uint256 _slotIndex,
        address _operator,
        uint256 _value
    ) external payable;

    /**
     * @notice Get the maximum value of a token that an operator is allowed to manage.
     * @param _tokenId The token for which to query the allowance
     * @param _slotIndex The slot for which to query the allowance
     * @param _operator The address of an operator
     * @return The current approval value of `_tokenId` that `_operator` is allowed to manage
     */
    function allowance(uint256 _tokenId, uint256 _slotIndex, address _operator) external view returns (uint256);

    /**
    * @dev The MFT transfers slot value to other MFTS
    * @param _fromTokenId MFT ID of the transaction initiator
    * @param _toTokenId MSFT ID of the receiver
    * @param _slotIndex Slot index
    * @param _valueOrNftId Number of ERC20 or ID of ERC721
    */
    function transferFrom(
        uint256 _fromTokenId,
        uint256 _toTokenId,
        uint256 _slotIndex,
        uint256 _valueOrNftId
    ) external payable;

    /**
    * @dev Slot transfers to EOA wallet address
    * @param _fromTokenId  MFT ID of the transaction initiator
    * @param _toAddress The recipient's wallet address
    * @param _slotIndex Slot index
    * @param _valueOrNftId Number of ERC20 or ID of ERC721
    */
    function transferFrom(
        uint256 _fromTokenId,
        address _toAddress,
        uint256 _slotIndex,
        uint256 _valueOrNftId
    ) external payable;
}

interface ERC165 {
    /**
    * @notice Query if a contract implements an interface
    * @param interfaceID The interface identifier, as specified in ERC-165
    * @dev Interface identification is specified in ERC-165. This function
    *  uses less than 30,000 gas.
    * @return `true` if the contract implements `interfaceID` and
    *  `interfaceID` is not 0xffffffff, `false` otherwise
    */
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}

interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Note that the caller is responsible to confirm that the recipient is capable of receiving ERC721
     * or else they may be permanently lost. Usage of {safeTransferFrom} prevents loss, though the caller must
     * understand this adds an external call which potentially creates a reentrancy vulnerability.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}
```

A wallet/broker/auction application MUST implement the **wallet interface** if it will accept safe transfers.

```solidity
/**
 * @title ERC-8000 token receiver interface
 * @dev Interface for a smart contract that wants to be informed by ERC-8000 contracts when
 *  receiving values from ANY addresses or EIP-3525 tokens.
 * Note: the EIP-165 identifier for this interface is 0xde7a9e53.
 */
interface IERC8000Receiver {
    /**
     * @notice Handle the receipt of an ERC-8000 token value.
     * @dev An ERC-8000 smart contract MUST check whether this function is implemented by the
     *  recipient contract, if the recipient contract implements this function, the ERC-8000
     *  contract MUST call this function after a value transfer (i.e. `transferFrom(uint256,
     *  uint256,uint256,,uint256,bytes)`).
     *  MUST return 0xde7a9e53 (i.e. `bytes4(keccak256('onERC8000Received(address,uint256,uint256,uint256,uint256,bytes)'))`) if the transfer is accepted.
     *  MUST revert or return any value other than 0x009ce20b if the transfer is rejected.
     * @param _operator The address which triggered the transfer
     * @param _fromTokenId The token id to transfer value from
     * @param _toTokenId The token id to transfer value to
     * @param _slotIndex The slot index to transfer value to
     * @param _valueOrNftId Number of ERC20 or ID of ERC721
     * @param _data Additional data with no specified format
     * @return `bytes4(keccak256('onERC8000Received(address,uint256,uint256,uint256,uint256,bytes)'))`
     *  unless the transfer is rejected.
     */
    function onERC8000Received(address _operator, uint256 _fromTokenId, uint256 _toTokenId, uint256 _slotIndex, uint256 _valueOrNftId, bytes calldata _data) external returns (bytes4);

}
```

The **metadata extension** is OPTIONAL for [ERC-721](./eip-721.md) smart contracts . This allows your smart
contract to be interrogated for its name and for details about the assets which your NFTs represent.

```solidity
/**
* @title ERC-8000 Semi-Fungible Token Standard, optional metadata extension
*  Note: the ERC-165 identifier for this interface is 0xe1600902.
*/
interface IERC8000Metadata is IERC8000, IERC721Metadata {
    /**
     * @notice Returns the Uniform Resource Identifier (URI) for the current contract.
     * @dev This function SHOULD return the URI for this contract in JSON format, starting with
     *  header `data:application/json;`.

     * @return The JSON formatted URI of the current ERC8000 contract
     */
    function contractURI() external view returns (string memory);

    /**
     * @notice Returns the Uniform Resource Identifier (URI) for the specified slot.
     * @dev This function SHOULD return the URI for `_slot` in JSON format, starting with header
     *  `data:application/json;`.
     * @return The JSON formatted URI of `_slot`
     */
    function slotURI(uint256 _slot) external view returns (string memory);
}
```

This is the "MFT Metadata JSON Schema for contractURI()" referenced above.

```json
{
  "title": "Asset Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset to which this NFT represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the asset to which this NFT represents"
    },
    "image": {
      "type": "string",
      "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
    },
    "external_link": {
      "type": "string",
      "description": "Optional. A URI pointing to an external resource."
    },
    "slot_size": {
      "type": "integer",
      "description": "Optional. Number of managed slots"
    }
  }
}
```

This is the "MFT Metadata JSON Schema for slotURI(uint)" referenced above.

```json
{
  "title": "Slot Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset category to which this slot represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the asset category to which this slot represents"
    },
    "image": {
      "type": "string",
      "description": "Optional. Either a base64 encoded imgae data or a URI pointing to a resource with mime type image/* representing the asset category to which this slot represents."
    },
    "transferable": {
      "type": "boolean",
      "description": "Whether the MFT can be transferred"
    },
    "is_token": {
      "type": "boolean",
      "description": "Token assets or not"
    },
    "is_nft": {
      "type": "boolean",
      "description": "True indicates NFT"
    },
    "token_address": {
      "type": "string",
      "description": "If isToken is true, it is the ERC20 OR ERC721 address, otherwise it is the zero address"
    },
    "decimals": {
      "type": "integer",
      "description": "The number of decimal places that the balance should display - e.g. 18, means to divide the token value by 1000000000000000000 to get its user representation."
    },
    "value": {
      "description": "The value of this slot, which may be a string or a number"
    }
  }
}
```

## Rationale

Each MFT on the Ethereum network has its unique properties. This EIP protocol, built upon [ERC-721](./eip-721.md), introduces the
concept of slots for each MFT. Each slot can store a specific type of asset, and the number of slots can be increased by
depositing [ERC-20](./eip-20.md) or [ERC-721](./eip-721.md) tokens into the contract.

Each MFT can act as a wallet to manage the slot assets associated with that MFT. These slot assets can be transferred to
other MFTs or Ethereum addresses. When an MFT is transferred to a new Ethereum address, the slot tokens it manages are
also transferred along with it, simplifying the asset migration process significantly.

**Approving Mechanism**

* This EIP provides additional slot authorization functionality in addition to being compatible with all authorization
  rules of [ERC-721](./eip-721.md):
    * `approve(uint256 _tokenId, uint256 _slotIndex, address _operator, uint256 value_)`:
        * MUST revert if `_tokenId` is zero id or `_operator` is zero address.
        * MUST revert if `_operator` is the same owner as the MFT
        * An authorized operator of the current owner of an NFT
    * For any approving function, the caller MUST be the owner or has been approved with a higher level of authority.

**Transfer Mechanism**

* The `transferFrom(uint256 _fromTokenId, uint256 _toTokenId, uint256 _slotIndex,uint256 _value)` function, SHOULD
  indicate slot value transfers from one MFT to another MFT, in accordance with the rules below:

    * MUST revert unless `msg.sender` is the owner of `_fromTokenId`, an authorized operator or an operator who has been
      approved the whole token or at least `_value` of it.
    * MUST revert if `_fromTokenId` or `_toTokenId` is zero token id or does not exist.
    * MUST revert if `_slotIndex`  is zero id or does not exist.
    * MUST revert if slots of `_fromTokenId` and `_toTokenId` do not match.
    * MUST revert if `_value` exceeds the value of `_fromTokenId` or its allowance to the operator.
    * MUST check for the `onERC8000Received` function if the owner of _toTokenId is a smart contract, if the function
      exists, MUST call this function after the value transfer, MUST revert if the result is not equal to `0xde7a9e53`;
    * MUST emit `TransferValue` event.

* The `transferFrom(uint256 _fromTokenId, address _to, uint256 _slotIndex,uint256 _value)` function, which transfers
  [ERC-20](./eip-20.md) or [ERC-721](./eip-721.md) value from one token ID to an address, SHOULD follow the rule below:

    * MUST revert unless `msg.sender` is the owner of `_fromTokenId`, an authorized operator or an operator who has been
      approved the whole token or at least `_value` of it.
    * MUST revert if `_fromTokenId` is zero token id or does not exist.
    * MUST revert if `_slotIndex`  is zero id or does not exist.
    * MUST revert if `_to` is zero address.
    * MUST revert if `_value` exceeds the value of `_fromTokenId` or its allowance to the operator.
    * MUST check for the `onERC8000Received` function if the _to address is a smart contract, if the function exists, MUST
      call this function after the value transfer, MUST revert if the result is not equal to `0xde7a9e53`;
    * MUST emit `TransferValue` event.

**[ERC-165](./eip-165.md) Interface**

We chose Standard Interface Detection ([ERC-165](./eip-165.md)) to expose the interfaces that a MFT smart contract supports.

## Backwards Compatibility

The EIP is backward compatible with [ERC-721](./eip-721.md)

## Reference Implementation

- [ERC-8000 implementation](../assets/erc-8000/contracts/ERC8000.sol)

## Security Considerations

The slot level approval (optional) is isolated from [ERC-721](./eip-721.md) approval models, so that approving value should not affect [ERC-721](./eip-721.md) level approvals. Implementations of this EIP must obey this principle.

Since this EIP is [ERC-721](./eip-721.md) compatible, any wallets and smart contracts that can hold and manipulate standard [ERC-721](./eip-721.md) tokens will have no risks of asset loss for [ERC-3525](./eip-3525.md) tokens due to incompatible standards implementations.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
