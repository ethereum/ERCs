---
eip: 8041
title: Fixed-Supply Agent NFT Collections
description: A standard for creating fixed-supply collections of ERC-8004 Agent NFTs with mint number tracking
author: Prem Makeig (@nxt3d)
discussions-to: https://ethereum-magicians.org/t/erc-8041-fixed-supply-agent-nft-collections/25656
status: Draft
type: Standards Track
category: ERC
created: 2025-10-11
requires: 8004, 7572
---

## Abstract

This standard defines an interface for creating fixed-supply collections of Agent NFTs that are registered in an [ERC-8004](./eip-8004.md) Agent Registry. While ERC-8004 provides an unlimited mint registry for agent identities, many use cases require limited collections. ERC-8041 provides a standard way to create such collections while maintaining the association between agents and their collection through onchain metadata and mint number tracking.

## Motivation

[ERC-8004](./eip-8004.md) establishes a singleton registry for AI Agent identity tokens with unlimited minting capability. This open registration model serves many use cases, but there is a clear need for:

1. **Fixed-supply collections**: Ability to create limited editions (e.g., "Genesis 100", "Season 1")
2. **Collection metadata**: Associate agents with specific collections and track their provenance
3. **Mint number tracking**: Permanent record of each agent's position in the collection (#1 of 1000)
4. **Time-gated releases**: Collections that activate at specific block numbers
5. **Curated drops**: Controlled minting by collection creators rather than open registration

This standard enables these use cases while leveraging the existing ERC-8004 registry infrastructure.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in [RFC 2119](https://www.rfc-editor.org/rfc/rfc2119.html) and [RFC 8174](https://www.rfc-editor.org/rfc/rfc8174.html).

### Collection Structure

Every compliant collection contract MUST maintain the following collection properties:

- `maxSupply`: Maximum number of agents that can exist in the collection
- `currentSupply`: Current number of agents minted in the collection
- `startBlock`: Block number when minting becomes available
- `open`: Boolean indicating if the collection is currently accepting mints

### Core Interface

Compliant collection contracts MUST implement the `IERC8041Collection` interface:

```solidity
pragma solidity ^0.8.25;

interface IERC8041Collection {
    /// @notice Emitted when the collection is created
    /// @param maxSupply Maximum number of agents in the collection
    /// @param startBlock Block number when minting becomes available
    event CollectionCreated(uint256 maxSupply, uint256 startBlock);

    /// @notice Emitted when an agent is minted in the collection
    /// @param agentId The ERC-8004 token ID of the agent in the registry
    /// @param mintNumber The agent's position in the collection (1-indexed)
    /// @param owner The address that received the agent
    event AgentMinted(uint256 indexed agentId, uint256 mintNumber, address indexed owner);

    /// @notice Get the mint number for a specific agent
    /// @param agentId The ERC-8004 token ID of the agent
    /// @return mintNumber The agent's position in the collection (1-indexed, e.g., "5 of 1000")
    ///         Returns 0 if the agent was not minted through this collection
    function getAgentMintNumber(uint256 agentId) external view returns (uint256 mintNumber);

    /// @notice Get the details of the collection
    /// @return maxSupply Maximum number of agents that can be minted
    /// @return currentSupply Current number of agents minted
    /// @return startBlock Block number when minting becomes available
    /// @return open Whether the collection is currently open for minting
    function getCollectionDetails() external view returns (
        uint256 maxSupply,
        uint256 currentSupply,
        uint256 startBlock,
        bool open
    );
}
```

### Required Functions

Every compliant collection contract MUST implement the following functions:

- `function getAgentMintNumber(uint256 agentId) external view returns (uint256 mintNumber)` - Returns the mint number for a given agent ID (0 if not in collection)
- `function getCollectionDetails() external view returns (uint256 maxSupply, uint256 currentSupply, uint256 startBlock, bool open)` - Returns the collection's configuration and current state

### Required Events

Every compliant collection contract MUST emit the following events:

- `event AgentMinted(uint256 indexed agentId, uint256 mintNumber, address indexed owner)` - MUST be emitted when an agent is added to the collection
- `event CollectionCreated(uint256 maxSupply, uint256 startBlock)` - MUST be emitted when the collection is created/initialized

### Required Behavior

#### Collection Association

When an agent is minted through a collection contract, the contract MUST:

1. Register the agent in the ERC-8004 registry
2. Assign a sequential mint number starting from 1
3. Store the agent's mint number internally
4. Write collection metadata to the agent's Onchain Metadata using the key `"agent-collection"`
5. Emit an `AgentMinted` event

The collection metadata format SHOULD be:
```solidity
abi.encodePacked(
    address collectionAddress,  // 20 bytes: the collection contract address
    uint8 mintNumberLength,     // 1 byte: length of mint number bytes
    bytes mintNumberBytes       // variable: the mint number (compact encoding)
)
```

This enables any party to:
- Look up which collection an agent belongs to
- Verify the collection contract address
- Decode the mint number from the metadata
- Query additional information from the collection contract

#### Mint Number Tracking

Mint numbers MUST:
- Start at 1 (not 0)
- Increment sequentially as agents are minted
- Be permanent and immutable once assigned
- Be queryable via `getAgentMintNumber()`

#### Supply Management

Collection contracts MUST:
- Enforce `maxSupply` limits (prevent minting beyond maximum)
- Track `currentSupply` accurately
- Emit `AgentMinted` events for each successful mint
- Respect the `startBlock` timing constraint

### Optional Features

The following features are OPTIONAL and left to implementation choice:

- **Access Control**: Who can mint (owner, public, allowlist, etc.)
- **Payment Model**: Free, paid (ETH/ERC-20), or other mechanisms
- **Collection State**: Open/close mechanisms, locking, pausing
- **Batch Minting**: Single or batch mint functions

#### ERC-7572: Contract-level Metadata

Collections SHOULD implement [ERC-7572](./eip-7572.md) contract metadata to provide collection-level information such as name, description, image, and external links. This is done by implementing:

```solidity
function contractURI() external view returns (string memory);
```

The URI should point to a JSON file following the [ERC-7572](./eip-7572.md) metadata schema.

## Rationale

ERC-8004 provides an open mint registry where anyone can register agent identities without restrictions. While this unlimited minting model serves many use cases, there is a clear need for limited-supply collections of agents. ERC-8041 addresses this by defining a standard interface for creating fixed-supply collections that leverage the existing ERC-8004 registry infrastructure while adding collection-specific constraints, metadata, and provenance tracking through mint numbers.

## Backwards Compatibility

This standard is fully backwards compatible with:
- **[ERC-721](./eip-721.md)**: Agents remain standard ERC-721 tokens
- **[ERC-8004](./eip-8004.md)**: Leverages existing registry infrastructure
- **[ERC-7572](./eip-7572.md)**: Optionally supports contract-level metadata

## Reference Implementation

A minimal reference implementation demonstrating the core requirements of ERC-8041:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

import {IERC8004AgentRegistry} from "./interfaces/IERC8004AgentRegistry.sol";
import {IERC8041Collection} from "./interfaces/IERC8041Collection.sol";

/**
 * @title ERC8041MinimalCollection
 * @notice Minimal reference implementation of ERC-8041
 * @dev Users supply pre-registered agent IDs to add to collection
 */
contract ERC8041MinimalCollection is IERC8041Collection {
    IERC8004AgentRegistry public immutable agentRegistry;
    
    uint256 public immutable maxSupply;
    uint256 public currentSupply;
    uint256 public immutable startBlock;
    bool public constant open = true;
    
    mapping(uint256 agentId => uint256 mintNumber) private _agentMintNumber;
    
    constructor(
        IERC8004AgentRegistry _agentRegistry,
        uint256 _maxSupply
    ) {
        agentRegistry = _agentRegistry;
        maxSupply = _maxSupply;
        startBlock = block.number;
        
        emit CollectionCreated(_maxSupply, block.number);
    }
    
    /**
     * @notice Add an existing agent to the collection
     * @param agentId The ID of an agent already registered in ERC-8004
     */
    function mint(uint256 agentId) external {
        require(currentSupply < maxSupply, "Supply exceeded");
        require(agentRegistry.ownerOf(agentId) == msg.sender, "Not agent owner");
        require(_agentMintNumber[agentId] == 0, "Already in collection");
        
        currentSupply++;
        uint256 mintNumber = currentSupply;
        _agentMintNumber[agentId] = mintNumber;
        
        // Store collection metadata in the agent's onchain metadata
        bytes memory mintNumberBytes = abi.encode(mintNumber);
        uint8 mintNumberLength = uint8(mintNumberBytes.length);
        bytes memory collectionMetadata = abi.encodePacked(
            address(this),      // 20 bytes: collection contract address
            mintNumberLength,   // 1 byte: length of mint number bytes
            mintNumberBytes     // variable: the mint number (compact encoding)
        );
        
        IERC8004AgentRegistry(address(agentRegistry)).setMetadata(
            agentId,
            "agent-collection",
            collectionMetadata
        );
        
        emit AgentMinted(agentId, mintNumber, msg.sender);
    }
    
    function getAgentMintNumber(uint256 agentId) external view returns (uint256) {
        return _agentMintNumber[agentId];
    }
    
    function getCollectionDetails() external view returns (
        uint256 _maxSupply,
        uint256 _currentSupply,
        uint256 _startBlock,
        bool _open
    ) {
        return (maxSupply, currentSupply, startBlock, open);
    }
}
```

This implementation demonstrates:
- **Single collection per contract**: Simplest deployment model
- **No access control**: Anyone can add their agents to the collection
- **Always open**: Collection is permanently open once deployed
- **Bring-your-own-agent model**: Users register agents separately, then add them to the collection
- **Full mint number tracking**: Each agent receives a sequential position number
- **Automatic start block**: Collection starts at deployment block

## Security Considerations

The owner of an ERC-8004 agent can modify or remove the "agent-collection" metadata stored on their agent at any time. Client applications MUST NOT rely solely on this metadata to verify collection membership. Instead, clients SHOULD:

1. Query the collection contract directly using `getAgentMintNumber(agentId)` to verify membership
2. Use the metadata as a convenience lookup to discover which collection to query, but always verify with the collection contract itself
3. Treat a mint number of `0` from the collection contract as definitive proof that an agent is not part of that collection

## Copyright

Copyright and related rights waived via CC0.

